#!/usr/bin/env sloth

let platform = ('uname'&!).stdout.trim()
let currentBranch = ('git symbolic-ref --short HEAD'&!).stdout.trim()

let shouldOpenChrome = $env['SSH_CLIENT'] == null and $env['SSH_TTY'] == null

let openCommand =
  if platform == 'Darwin' {
    'open'
  } else if platform == 'Linux' {
    'xdg-open'
  } else {
    assert(false, "Oops! You're on ${platform}. I don't know what that is...")
  }

let remote = if $argv.length > 0 {
  $argv[0]
} else {
  ("git remote -v" |
  ['sed', '-n', 's/^\([a-zA-Z_-]\+\)\t\+\([^ ]\+\).*$/\1\t\2/p'] |
  "uniq" |
  "tac" |
  "fzf" |
  "sed -n 's/^\([a-zA-Z_-]\+\).*/\1/p'"&!).
    stdout.
    trim()
}

let pipe = Pipe.new()
with ($stderr = pipe.write, $stdout = pipe.write) {
  "git push --porcelain --set-upstream ${remote} HEAD"!
}

pipe.write.close()
let out = pipe.read.readAll()

if currentBranch == 'master' or currentBranch == 'main' {
  exit(0)
}

print("The output was \"${out}\"")
